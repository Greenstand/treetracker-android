plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'com.google.firebase.crashlytics'
}

def loadExtraProperties(String fileName) {
    def props = new Properties()
    props.load(new FileInputStream(fileName))
    props.each { key, val ->
        project.setProperty(key, val)
    }
}

loadExtraProperties("treetracker.keys.properties")

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        applicationId "org.greenstand.android.TreeTracker"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 197
        versionName "2.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_production_identity_pool_id}\""
        buildConfigField "String", "OBJECT_STORAGE_IDENTITY_REGION", '"eu-central-1"'
        buildConfigField 'String', 'OBJECT_STORAGE_ENDPOINT', '"eu-central-1"'
        buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-production-images"'
        buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-production-batch-uploads"'
        buildConfigField 'String', 'API_GATEWAY', "https://dev-k8s.treetracker.org"
        buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${prod_treetracker_client_id}\""
        buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${prod_treetracker_client_secret}\""
        buildConfigField 'Boolean', 'TREE_HEIGHT_FEATURE_ENABLED', "false"
        buildConfigField 'Boolean', 'TREE_NOTE_FEATURE_ENABLED', "true"
        buildConfigField 'Boolean', 'TREE_DBH_FEATURE_ENABLED', "false"
        buildConfigField 'Boolean', 'AUTOMATIC_SIGN_OUT_FEATURE_ENABLED', "true"
        buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "false"
        buildConfigField 'Boolean', 'USE_AWS_S3', "false"
        buildConfigField 'Boolean', 'ORG_LINK', "false"


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        compose true
        viewBinding true

        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = "11" //"1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11//VERSION_1_8
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled = true
    }


    buildTypes {
        release {
            minifyEnabled true
            resValue "string", "app_name", "Tree Tracker"
            buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_production_identity_pool_id}\""
            buildConfigField 'String', 'OBJECT_STORAGE_ENDPOINT', '"eu-central-1"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-production-images"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-production-batch-uploads"'
            buildConfigField 'Boolean', 'USE_AWS_S3', "true"
            buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "false"
            buildConfigField 'String', 'API_GATEWAY', '"https://prod-k8s.treetracker.org"'
            buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${prod_treetracker_client_id}\""
            buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${prod_treetracker_client_secret}\""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }


        prerelease {
            initWith(debug)
            applicationIdSuffix ".prerelease"
            buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_production_identity_pool_id}\""
            buildConfigField 'String', 'OBJECT_STORAGE_ENDPOINT', '"eu-central-1"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-production-images"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-production-batch-uploads"'
            buildConfigField 'Boolean', 'USE_AWS_S3', "true"
            buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "false"
            resValue "string", "app_name", "Tree Tracker Prerelease"
            buildConfigField 'String', 'API_GATEWAY', '"https://prod-k8s.treetracker.org"'
            buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${prod_treetracker_client_id}\""
            buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${prod_treetracker_client_secret}\""
        }

        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Tree Tracker(debug)"
            buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_test_identity_pool_id}\""
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-test-images"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-test-batch-uploads"'
            buildConfigField 'Boolean', 'USE_AWS_S3', "true"
            buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "true"
            buildConfigField 'String', 'API_GATEWAY', '"https://dev-k8s.treetracker.org"'
            buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${test_treetracker_client_id}\""
            buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${test_treetracker_client_secret}\""
        }

        beta {
            applicationIdSuffix ".test"
            resValue "string", "app_name", "Tree Tracker(test)"
            buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_test_identity_pool_id}\""
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-test-images"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-test-batch-uploads"'
            buildConfigField 'Boolean', 'USE_AWS_S3', "true"
            buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "false"
            buildConfigField 'String', 'API_GATEWAY', '"https://test-k8s.treetracker.org"'
            buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${test_treetracker_client_id}\""
            buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${test_treetracker_client_secret}\""
            signingConfig signingConfigs.debug
            debuggable true
        }

        dev {
            initWith(debug)
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Tree Tracker(dev)"
            buildConfigField "String", "OBJECT_STORAGE_IDENTITY_POOL_ID", "\"${s3_dev_identity_pool_id}\""
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_IMAGES', '"treetracker-dev-images"'
            buildConfigField 'String', 'OBJECT_STORAGE_BUCKET_BATCH_UPLOADS', '"treetracker-dev-batch-uploads"'
            buildConfigField 'Boolean', 'BLUR_DETECTION_ENABLED', "true"
            buildConfigField 'String', 'API_GATEWAY', '"https://dev-k8s.treetracker.org"'
            buildConfigField "String", "TREETRACKER_CLIENT_ID", "\"${dev_treetracker_client_id}\""
            buildConfigField "String", "TREETRACKER_CLIENT_SECRET", "\"${dev_treetracker_client_secret}\""

        }
    }

    useLibrary 'org.apache.http.legacy'

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'

    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'org.greenstand.android.TreeTracker'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11" //"1.8"
    }
}

repositories {
    maven {
        url "https://maven.google.com"
    }
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

dependencies {

    // AWS S3 SDK
    // Note: android s3 sdk does not support transfer acceleration
    implementation libs.aws.android.sdk.core
    implementation libs.aws.android.sdk.s3

    // Koin dependencies
    implementation libs.bundles.koin
    testImplementation libs.koin.test

    // AndroidX Core
    implementation libs.androidx.appcompat
    implementation libs.androidx.multidex
    implementation libs.androidx.exifinterface
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.work.runtime.ktx

    // Compose
    implementation libs.bundles.compose
    implementation libs.accompanist.permissions

    // MapLibre
    implementation libs.maplibre.android.sdk

    // Image Loading
    implementation libs.coil.compose

    // Kotlin
    implementation libs.kotlin.stdlib.jdk7
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.datetime
    implementation libs.kotlinx.serialization.core
    coreLibraryDesugaring libs.desugar.jdk.libs

    // Database
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    kapt libs.androidx.room.compiler

    // Debug database for dev builds
    devImplementation libs.debug.database

    // Networking
    api libs.retrofit.converter.gson
    implementation libs.retrofit
    implementation libs.okhttp.logging.interceptor

    // CameraX
    implementation libs.bundles.camerax

    // Logging
    api libs.timber
    implementation libs.androidx.legacy.support.v4

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics
    implementation libs.firebase.crashlytics.ktx
    implementation libs.firebase.iid

    // Testing
    testImplementation libs.bundles.testing
    testImplementation libs.androidx.room.testing
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.androidx.arch.core.testing
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.turbine
    testImplementation libs.turbine
}

configurations.all {
    resolutionStrategy {
        preferProjectModules()
        force libs.gson
    }
}

apply plugin: 'com.google.gms.google-services'

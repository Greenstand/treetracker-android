buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('keys.json')
        }
    }
    defaultConfig {
        applicationId "org.greenstand.android.TreeTracker"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 19
        versionName "1.1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    buildTypes {
        release {
            minifyEnabled true
            resValue "string", "app_name", "Tree Tracker"
            buildConfigField 'String', 'BASE_URL', '"https://www.treetracker.org"'
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            buildConfigField "String", "DB_PATH", '"/data/data/org.greenstand.android.TreeTracker/databases/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Tree Tracker(debug)"
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            // Change this back to 'off' and use QA build instead
            buildConfigField 'String', 'BASE_URL', '"http://174.138.107.11"'
            buildConfigField "String", "DB_PATH", '"/data/data/org.greenstand.android.TreeTracker.debug/databases/"'
        }
        dev {
            initWith(debug)
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Tree Tracker(dev)"
            buildConfigField "String", "DB_PATH", '"/data/data/org.greenstand.android.TreeTracker.dev/databases/"'
        }
        qa {
            initWith(debug)
            applicationIdSuffix ".qa"
            resValue "string", "app_name", "Tree Tracker(qa)"
            buildConfigField "String", "GPS_ACCURACY", '"on"'
            buildConfigField 'String', 'BASE_URL', '"http://test.treetracker.org"'
            buildConfigField "String", "DB_PATH", '"/data/data/org.greenstand.android.TreeTracker.qa/databases/"'
        }
    }
    productFlavors {

    }
    useLibrary 'org.apache.http.legacy'
}

repositories {
    maven {
        url "https://maven.google.com"
    }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'

    })

    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"

    //compile 'junit:junit:4.12'
    //testCompile 'junit:junit:4.12'
    //androidTestCompile 'junit:junit:4.12'

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.android.gms:play-services:11.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
    compile 'com.jakewharton.timber:timber:4.6.0'
    compile 'com.amazonaws:aws-android-sdk-core:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.+'
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    compile 'com.google.firebase:firebase-core:11.8.0'

}
play {
    track = 'beta' // or 'rollout' or 'production' or 'alpha'
}
//apply plugin: 'com.google.gms.google-services'

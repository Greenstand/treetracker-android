// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        classpath libs.android.gradlePlugin
        classpath libs.google.services
        classpath libs.firebase.crashlytics.gradle
        classpath libs.kotlin.gradlePlugin
        classpath libs.navigation.safeargs.gradlePlugin

        classpath libs.ktlint.gradle
        classpath libs.spotless.gradle
        classpath libs.detekt.gradlePlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    apply plugin : "org.jlleitschuh.gradle.ktlint"
    ktlint {
        android.set(true)
        verbose.set(true)
        filter {
            exclude { element -> element.file.path.contains("generated/") }
        }
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url = "https://maven.google.com" // Google's Maven repository
        }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url = 'https://jitpack.io' }
    }

    ext {
        // Keys properties - these still need to be loaded from treetracker.keys.properties
        s3_production_identity_pool_id = "configure in treetracker.keys.properties"
        prod_treetracker_client_id = "configure in treetracker.keys.properties"
        prod_treetracker_client_secret = "configure in treetracker.keys.properties"
        s3_test_identity_pool_id = "configure in treetracker.keys.properties"
        test_treetracker_client_id = "configure in treetracker.keys.properties"
        test_treetracker_client_secret = "configure in treetracker.keys.properties"
        s3_dev_identity_pool_id = "configure in treetracker.keys.properties"
        dev_treetracker_client_id = "configure in treetracker.keys.properties"
        dev_treetracker_client_secret = "configure in treetracker.keys.properties"
        treetracker_client_id = "configure in treetracker.keys.properties"
        treetracker_client_secret = "configure in treetracker.keys.properties"
    }
}

subprojects {
    apply plugin : "io.gitlab.arturbosch.detekt"
    detekt {
        config = files("${project.rootDir}/detekt.yml")
        parallel = true
        buildUponDefaultConfig = true
    }
    apply plugin : "com.diffplug.spotless"
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("${project.rootDir}/build-logic/**/*.kt")
            licenseHeaderFile(
                    rootProject.file("${project.rootDir}/spotless/copyright.kt"),
                    "^(package|object|import|interface)"
            )
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}